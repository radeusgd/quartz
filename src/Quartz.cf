comment "//" ;
comment "/*" "*/" ;

position token QIdent (letter (letter | digit | '_')*) ;
token CustomOperator ('>' | '<' | '=' | '$' | '!')('?' | '>' | '<' | '=' | '$' | '!' | '+' | '*' | '/' | '-')* | ('(''+'')') | ('(''*'')') | ('(''/'')') | ('(''-'')');

Prog. Program ::= [Declaration] ;

Func. Declaration ::= "def" QIdent [TypeQualifier] "(" [Arg] ")" ":" Type "=" Exp ;
ParameterlessFunc. Declaration ::= "def" QIdent [TypeQualifier] "()" ":" Type "=" Exp ;
-- FuncDefault. Declaration ::= "def" QIdent "(" [Arg] ")" "=" Exp ;
Operator. Declaration ::= "defop" CustomOperator [TypeQualifier] "(" Arg "," Arg ")" ":" Type "=" Exp ;
Value. Declaration ::= "val" QIdent ":" Type "=" Exp ;
ValueInferred. Declaration ::= "val" QIdent "=" Exp ;
Import. Declaration ::= "import" QIdent ;
Data. Declaration ::= "data" QIdent [QIdent] "=" [DataCase] ; -- TODO type polymorphism

DataConstructor. DataCase ::= QIdent [Type] ;
separator Type "*" ;
separator nonempty DataCase "|" ;

Argument. Arg ::= QIdent ":" Type ;
ArgumentWithDefault. Arg ::= QIdent ":" Type "=" Exp ;

FreeTypeVariable. TypeQualifier ::= "[" QIdent "]" ;
-- RestrictedTypeVariable. TypeQualifier ::= "[" QIdent ":" [TypeBound] "]" ;
separator TypeQualifier "" ;

-- SimpleTypeBound. TypeBound ::= QIdent ;
-- separator nonempty TypeBound "," ;

terminator Declaration ";" ;
separator  Arg "," ;

Atom. Type2 ::= QIdent;
UnitAtom. Type2 ::= "()" ;
Abstraction. Type ::= Type2 "->" Type ;
Constructor. Type ::= QIdent [Type2] ;
separator nonempty Type2 "" ;
coercions Type 2 ;
-- TODO complex types, typeclasses

EApp. Exp1 ::= Exp Exp2 ;
ECustomOp. Exp1 ::= Exp1 CustomOperator Exp2 ;
EIfThenElse. Exp1 ::= "if" Exp2 "then" Exp2 "else" Exp2 ;
ELambda. Exp1 ::= "\\" QIdent "->" Exp2 ;
EAdd. Exp2 ::= Exp2 "+" Exp3 ;
ESub. Exp2 ::= Exp2 "-" Exp3 ;
EMul. Exp3 ::= Exp3 "*" Exp4 ;
EDiv. Exp3 ::= Exp3 "/" Exp4 ;
EVar. Exp4 ::= QIdent ;
EStr. Exp4 ::= String ;
EInt. Exp4 ::= Integer ;
ENegInt. Exp1 ::= "-" Integer ;
EDouble. Exp4 ::= Double ;
ENegDouble. Exp1 ::= "-" Double ;
ETrue. Exp4 ::= "true" ;
EFalse. Exp4 ::= "false" ;
EUndefined. Exp4 ::= "???" ;
EUnit. Exp4 ::= "()" ;
EBlock. Exp4 ::= "{" [Declaration] Exp "}" ;
EList. Exp4 ::= "[" [Exp] "]" ;
EMatch. Exp4 ::= "case" Exp "of" "{" [Case] "}" ;
EDo. Exp4 ::= "do" "{" [DoClause] "}" ;
separator Exp "," ;
coercions Exp 4 ;

SimpleCase. Case ::= "|" QIdent [QIdent] "->" Exp ;
separator Case "" ;
separator QIdent "" ;

DoExp. DoClause ::= Exp ;
DoLet. DoClause ::= QIdent "<-" Exp ;
separator nonempty DoClause ";"